<!--#include virtual="/lib/include/html/template/template_change.asp"-->
<!--#include file="include/include_all_message.inc"-->
<!--#include file="functions/functions_reservation_class.asp"-->
<%
    'TODO: 想偷雞取消過期課程的客戶

    'TODO: 預定失敗的判斷及錯誤訊息

    'TODO: 發信

    'TODO: 選到已經預定的課程的處理?
    
    Dim str_client_full_ename : str_client_full_ename = ""                              '客戶英文名字
    Dim str_client_login_email_addr : str_client_login_email_addr = ""                  '客戶登入的帳號
    Dim int_client_sn, str_client_ip                                                    '客戶編號、客戶的IP
    Dim flt_refund_session : flt_refund_session = 0.0                                   '要歸還的堂數
    Dim str_attend_list_sn : str_attend_list_sn = Trim(Request("attend_list_sn"))       'client_attend_list.sn
    Dim str_attend_list_date : str_attend_list_date = Trim(Request("attend_list_date")) 'client_attend_list.attend_date
    Dim str_attend_list_time : str_attend_list_time = Trim(Request("attend_list_time")) 'client_attend_list.attend_sestime    
    Dim str_cancel_success_msg_for_email : str_cancel_success_msg_for_email = ""        '訂課成功後 寄送給客戶的新增課程訊息
    Dim int_class_type : int_class_type = null                                          '課程類型(一對一或一對多或大會堂)
    Dim flt_client_total_session_last_get : flt_client_total_session_last_get = null    '客戶剩餘堂數
    Dim str_sql, arr_param_value, arr_attend_list_data, int_update_result
    Dim dat_attend_date, int_attend_sestime, str_class_type_description, str_cancel_success_msg, str_cancel_fault_msg

    str_cancel_success_msg = ""
    str_cancel_fault_msg = ""

    '取得客戶編號
    if (g_var_client_sn <> "") then
        int_client_sn = g_var_client_sn
    '沒有客戶編號?
    else
        Response.Write "沒有客戶編號"
        Response.End
    end if


    '客戶資料 COM 物件
    Dim obj_member_opt_exe : Set obj_member_opt_exe = Server.CreateObject("TutorLib.MemberOperation.ClientDataOpt")
    Dim int_member_result_exe : int_member_result_exe = obj_member_opt_exe.prepareData(g_var_client_sn, CONST_VIPABC_RW_CONN)
    if (int_member_result_exe = CONST_FUNC_EXE_SUCCESS) then
        flt_client_total_session = obj_member_opt_exe.getData(CONST_CUSTOMER_REMAIN_CONTRACT_POINT)             '取得客戶總剩餘堂數
        str_client_full_ename = obj_member_opt_exe.getData("fname") & " " & obj_member_opt_exe.getData("lname") '取得客戶英文名字        
        str_client_login_email_addr = obj_member_opt_exe.getData("email")                                       '取得客戶登入的帳號
    else
        '錯誤訊息
        'Response.Write "取不到客戶的剩餘堂數 member prepareData error = " & obj_member_opt_exe.PROCESS_STATE_RESULT & "<br>"
    end if
    Set obj_member_opt_exe = Nothing

    'TODO: 若取不到客戶的剩餘堂數
    'if (isEmptyOrNull(flt_client_total_session)) then
        'Response.Write "若取不到客戶的剩餘堂數"
    'end if


    '取得客戶IP位置
    str_client_ip = getIpAddress()
    str_client_ip = CStr(str_client_ip)


    '先確定客戶真的有訂這堂課 且 訂課日期不小於今日 在取消
    str_sql = "SELECT sn, svalue, CONVERT(varchar, attend_date, 111) AS sdate, attend_sestime, attend_livesession_types, attend_level FROM client_attend_list WHERE " &_
              "(client_sn = @client_sn) AND (sn = @sn) AND (valid = 1) AND (CONVERT(varchar, attend_date, 111) >= CONVERT(varchar, getdate(), 111))"
    arr_param_value = Array(int_client_sn, str_attend_list_sn)            
    arr_attend_list_data = excuteSqlStatementRead(str_sql, arr_param_value, CONST_VIPABC_RW_CONN)
    if (isSelectQuerySuccess(arr_attend_list_data)) then
        if (Ubound(arr_attend_list_data) >= 0) then

            '要歸還的堂數
            flt_refund_session = arr_attend_list_data(1, 0)

            dat_attend_date = arr_attend_list_data(2, 0) 
            int_attend_sestime = arr_attend_list_data(3, 0) 

            str_sql = "UPDATE client_attend_list SET attend_consultant = null, attend_room = null, " &_
                      "attend_mtl_1 = null, attend_mtl_2 = null, session_sn = null, " &_
                      "attend_level = null, valid = 0, view_ip = @view_ip, " &_
                      "special_sn = null, attend_datetime = @attend_datetime " &_
                      "WHERE (client_sn = @client_sn) AND (sn = @sn) AND (valid = 1)"
            arr_param_value = Array(str_client_ip, getFormatDateTime(now(), 2), int_client_sn, str_attend_list_sn)

            int_update_result = excuteSqlStatementWrite(str_sql, arr_param_value, CONST_VIPABC_RW_CONN)

            if (int_update_result >= 0) then
                '更新要顯示的取消成功訊息
                if (arr_attend_list_data(5, 0) > 12) then
                    str_class_type_description = "大会堂"
                    int_class_type = CONST_CLASS_TYPE_LOBBY
                elseif (Int(arr_attend_list_data(4, 0)) = CONST_CLASS_TYPE_NORMAL) then
                    str_class_type_description = "小班(1-6人)"
                    int_class_type = CONST_CLASS_TYPE_NORMAL
                elseif (Int(arr_attend_list_data(4, 0)) = CONST_CLASS_TYPE_ONE_ON_ONE) then
                    str_class_type_description = "1对1"
                    int_class_type = CONST_CLASS_TYPE_ONE_ON_ONE
                end if

                '更新取消課程成功的訊息
                str_cancel_success_msg = "<span class=""bold"">" & dat_attend_date & " " & int_attend_sestime & ":30 " & str_class_type_description & "</span>"

                '更新要寄送email給客戶的取消課程成功 課程訊息
                str_cancel_success_msg_for_email = str_cancel_success_msg_for_email & getClassMsgForEmail(int_class_type, dat_attend_date, int_attend_sestime, "cancel") & "<br>"

                if (DateDiff("n",now(), dat_attend_date & " " & int_attend_sestime & ":30:00") <= 1440) then
                    '更新各時段，各身份可上課狀態
                    Call updateBookingStatus(dat_attend_date, int_attend_sestime, CONST_VIPABC_RW_CONN)
                end if
            else
                Response.Write("錯誤訊息" & g_str_run_time_err_msg & "<br>")

                '更新取消課程失敗的訊息
                str_cancel_fault_msg = "<span class=""bold"">" & str_attend_list_date & " " & str_attend_list_time & ":30 " & "</span> (取消失敗)"
            end if
        else
            '更新取消課程失敗的訊息
            str_cancel_fault_msg = "此堂課您並沒有預定 (取消失敗)"
        end if
    else
        '更新取消課程失敗的訊息
        str_cancel_fault_msg = "取消課程失敗"
    end if


    '失敗的訊息
    if (Not isEmptyOrNull(Trim(str_cancel_fault_msg))) then
        Response.Write Trim(str_cancel_fault_msg)
    '若有取消課程成功的話 就發信給客戶
    elseif (Not isEmptyOrNull(Trim(str_cancel_success_msg))) then
        Call sendOrderCancelEmail(str_client_login_email_addr, str_client_full_ename, str_cancel_success_msg_for_email, "cancel")


        '在取一次客戶的總剩餘堂數 目的是為了處理同時有多筆cancel發生
        '客戶資料 COM 物件
        Set obj_member_opt_exe = Server.CreateObject("TutorLib.MemberOperation.ClientDataOpt")
        int_member_result_exe = obj_member_opt_exe.prepareData(g_var_client_sn, CONST_VIPABC_RW_CONN)
        if (int_member_result_exe = CONST_FUNC_EXE_SUCCESS) then
            flt_client_total_session_last_get = obj_member_opt_exe.getData(CONST_CUSTOMER_REMAIN_CONTRACT_POINT)             '取得客戶總剩餘堂數        
        end if
        Set obj_member_opt_exe = Nothing

        '剩餘堂數和取消前不一樣 代表課程已經從table刪除了
        if (flt_client_total_session_last_get <> flt_client_total_session) then
            flt_client_total_session = flt_client_total_session_last_get
        '剩餘堂數和取消前一樣 代表課程還未從table刪除(update 可能沒那麼立即)
        else
            flt_client_total_session = Round(flt_client_total_session + flt_refund_session, 2)
        end if

        Response.Write "您已成功取消" & chr(10)
        Response.Write str_cancel_success_msg & "課程1堂" & chr(10)
        Response.Write "共计归还" & flt_refund_session & "课时数 可用课时数" & flt_client_total_session
    end if
%>
<!--內容start-->
<!--<div class="okbox">
    <div class="ok">
        <%
            if (Not isEmptyOrNull(Trim(str_cancel_success_msg))) then
                Response.Write "您已成功取消"
                Response.Write str_cancel_success_msg & "課程1堂"
            end if

            '失敗的訊息
            if (Not isEmptyOrNull(Trim(str_cancel_fault_msg))) then
                Response.Write Trim(str_cancel_fault_msg)
            end if
        %>
        <br/>
        共计归还 <span class="bold"><%=flt_refund_session%></span>课时数  可用课时数 <span class="bold"><%=Round(flt_client_total_session + flt_refund_session, 2)%></span>
    </div>
    <div class="btn">
        <input type="button"name="button" id="button" value="+ 返回预订课程" class="btn_1" onclick="javascript:location.href='/program/member/reservation_class/normal_and_vip.asp';" />
        <input type="button" name="button" id="button" value="+ 查看课表" class=" m-left5 btn_1" onclick="javascript:location.href='/program/member/reservation_class/watch_class.asp';" />
    </div>
    <font id="<%=CONST_HTML_MAIN_CONTENTS_DIV_ID%>"></font>
</div>-->
<!--內容end-->